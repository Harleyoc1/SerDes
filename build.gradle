plugins {
    id 'java'
    id 'maven-publish'
}

ext.configFile = file "gradle.properties"
configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

group config.package_group
version config.version

java.toolchain.languageVersion = JavaLanguageVersion.of(16)

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        name 'Harley O\'Connor Maven'
        url 'http://harleyoconnor.com/maven/'
        allowInsecureProtocol = true
    }
}

dependencies {
    implementation group: 'com.google.guava', name: 'guava', version: config.guava_version
    implementation group: 'com.harleyoconnor.javautilities', name: 'JavaUtilities', version: config.javautilities_version

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: config.junit_version
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = config.name

            from components.java

            pom {
                url = "https://github.com/Harleyoc1/${config.name}"
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://mit-license.org'
                    }
                }
                developers {
                    developer {
                        id = 'harleyoconnor'
                        name = 'Harley O\'Connor'
                        email = 'harleyoc1@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/Harleyoc1/${config.name}.git"
                    developerConnection = "scm:git:ssh://github.com/Harleyoc1/${config.name}.git"
                    url = "https://github.com/Harleyoc1/${config.name}"
                }
            }
        }
    }
}